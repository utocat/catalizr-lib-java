buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

plugins {
    id "java"
    id "maven"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'com.google.code.gson:gson:2.8.2'
	compile 'com.utocat:catalizr-lib-java:0.0.1'
	
	testCompile 'org.testng:testng:6.14.2'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
}

test {
	useTestNG {
         suites 'src/test/resources/testng.xml'
     }
    systemProperty "https.protocols", "TLSv1.2,TLSv1.1,TLSv1"
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces true
    }
}

apply from: 'gradle/publish.gradle'

group = 'com.utocat'
version = '0.0.4'
sourceCompatibility = '1.6'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task updatePropertiesFile {
    doLast {
        logger.quiet('Update Catalizr Properties File')
        def propertyFile = file "src/main/resources/com/utocat/catalizr/core/catalizr.properties"
        def props = new Properties()
        propertyFile.withReader { props.load(it) }
        props.setProperty("version", (String) version)
        propertyFile.withWriter { props.store(it, null) }
    }
}

processResources.dependsOn updatePropertiesFile
